plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-parcelize'
}

android {
    namespace 'br.com.arch.movieapp'
    compileSdk 33

    def credentials = rootProject.file("local.properties")
    def credentialProperty = new Properties()
    credentialProperty.load(new FileInputStream(credentials))

    defaultConfig {
        applicationId "br.com.arch.movieapp"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        buildConfigField "String", "TMDB_API_KEY", credentialProperty.getProperty("TMDB_API_KEY")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        buildConfig = true
        viewBinding true
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation libs.bundles.networking
    implementation libs.bundles.androidSupport
    implementation libs.bundles.lifecycle
    implementation libs.hilt.android
    implementation libs.glide
    implementation libs.glide.okhttp
    kapt libs.glide.compiler
    kapt libs.hilt.compiler
    kapt libs.hilt.androidx.compiler

}